import "util.scrypt";

/**
 * token protocol payload parser
 *
 * PayloadToken负责解码/编码Token合约的数据。构造函数解码，dump函数编码。
 *
 * bsv合约可以由脚本代码部分和数据部分组成，两部分之间用 OP_RETURN 操作符隔开。
 * 当锁定脚本执行到OP_RETURN时便会终止，其后的数据部分不会被处理。
 * 但当前数据部分依然是一条或多条正常的OP_PUSHDATAX语句，附带着确定数量的字节内容。
 *
 * Token合约的数据格式设计如下：
 *
 * 最后一个字节为数据格式类型，分为4种：ISSUE、TRANSFER、SWAP、SELL
 *
 * 1. codePart 为合约代码。结尾还包括2字节额外内容：OP_RETURN操作符 和 OP_PUSHDATA(一个字节0x28)，表示接下来有40字节的GenesisPart。
 * 2. GenesisPart 为合约溯源数据。包括genesisTx的 某utxo的outpoint，以及创建合约的outputIndex。共 32+4+4=40 字节。
 * 3. 其后再包括一个OP_PUSHDATA(dataPrefix),可能有多字节，取决于其他数据data的长度。
 * 4. 接下来便是其他数据部分聚合，比如：pubkeyHash有20字节，tokenAmount有8字节，satoshiAmount有8字节。
 *
 *  codePart, GenesisPart, blockHeader, blockHeight, MINT
 *  codePart, GenesisPart, ownerPkh, tokenAmount, TRANSFER
 *  codePart, GenesisPartA, ownerPkhA, tokenAmountA, GenesisPartB, tokenAmountB, SWAP
 *  codePart, GenesisPart, ownerPkh, tokenAmount, satoshiAmount, SELL
 */

struct PayloadTokenData {
  /* 合约最后一块数据部分开始的字节下标，在第二个OP_PUSHDATA之后 */
  int dataStart;

  /* 合约代码的内容，末尾包括 OP_RETURN 和 0x28 */
  bytes codePart;

  /* 包括合约代码内容，OP_RETURN 和 0x28，再加上GenesisPart内容，但不包括后续的 OP_PUSHDATA */
  bytes codeWithGenesisPart;

  /* 数据类型，1字节 */
  bytes dataType;

  /* 溯源需匹配genesisTx input的outpoint 和output的index */
  bytes genesisOutpoint;
  bytes genesisOutputIdx;

  /* mint 数据 */
  bytes blockHeader;
  int blockHeight;

  /* transfer 数据 */
  Ripemd160 ownerPkh;
  int amount;

  /* swap 数据 */
  bytes genesisOutpointSwap;
  bytes genesisOutputIdxSwap;
  int amountSwap;

  /* sell 数据 */
  int satoshiAmountSell;
}

library PayloadToken {
  /* 不同类型数据部分的长度 */
  static int genesisLen = 40;
  static int mintLen = 89;
  static int transferLen = 29;
  static int swapLen = 77;
  static int sellLen = 37;

  /* OP_PUSHDATA 协议推送不同长度数据时，需要的前缀字节 */
  static bytes mintPrefix = b'4c59';
  static bytes transferPrefix = b'1d';
  static bytes swapPrefix = b'4c4d';
  static bytes sellPrefix = b'25';

  /* 数据类型dataType标记 */
  static bytes MINT = b'00';
  static bytes TRANSFER = b'01';
  static bytes SWAP = b'02';
  static bytes SELL = b'03';

  static function load(bytes scriptCode): PayloadTokenData {
      PayloadTokenData self = {
          0,                        // int dataStart;
          b'00',                    // bytes codePart;
          b'00',                    // bytes codeWithGenesisPart;
          b'00',                    // bytes dataType;
          b'00',                    // bytes genesisOutpoint;
          b'00',                    // bytes genesisOutputIdx;
          b'00',                    // bytes blockHeader;
          0,                        // int blockHeight;
          Ripemd160(b'00'),         // Ripemd160 ownerPkh;
          0,                        // int amount;
          b'00',                    // bytes genesisOutpointSwap;
          b'00',                    // bytes genesisOutputIdxSwap;
          0,                        // int amountSwap;
          0                         // int satoshiAmountSell;
      };

    int scriptLen = len(scriptCode);
    self.dataType = scriptCode[scriptLen - 1 : ];

    int dataLen = 0;
    int prefixLen = 0;
    if (self.dataType == PayloadToken.MINT) {
      dataLen = PayloadToken.mintLen;
      prefixLen = len(PayloadToken.mintPrefix);
    }
    else if (self.dataType == PayloadToken.TRANSFER) {
      dataLen = PayloadToken.transferLen;
      prefixLen = len(PayloadToken.transferPrefix);
    }
    else if (self.dataType == PayloadToken.SWAP) {
      dataLen = PayloadToken.swapLen;
      prefixLen = len(PayloadToken.swapPrefix);
    }
    else if (self.dataType == PayloadToken.SELL) {
      dataLen = PayloadToken.sellLen;
      prefixLen = len(PayloadToken.sellPrefix);
    }
    /* part */
    self.dataStart = scriptLen - dataLen;
    self.codeWithGenesisPart = scriptCode[ : self.dataStart - prefixLen ];
    int codeWithGenesisLen = len(self.codeWithGenesisPart);
    self.codePart = self.codeWithGenesisPart[ : codeWithGenesisLen - PayloadToken.genesisLen];
    /* genesis */
    self.genesisOutpoint = self.codeWithGenesisPart[codeWithGenesisLen - PayloadToken.genesisLen : codeWithGenesisLen - 4];
    self.genesisOutputIdx = self.codeWithGenesisPart[codeWithGenesisLen - 4 : ];
    /* data: */
    bytes dataPart = scriptCode[self.dataStart : ];

    /* support many kinds of data type */
    if (self.dataType == PayloadToken.MINT) {
      self.blockHeader = dataPart[ : 80];
      self.blockHeight = unpack(dataPart[80 : 80 + 8]);
    } else {
      self.ownerPkh = Ripemd160(dataPart[ : 20]);
      self.amount = unpack(dataPart[20 : 20 + 8]);
    }

    if (self.dataType == PayloadToken.SWAP) {
      self.genesisOutpointSwap = dataPart[28 : 28 + 36];
      self.genesisOutputIdxSwap = dataPart[64 : 64 + 4];
      self.amountSwap = unpack(dataPart[68 : 68 + 8]);
    }
    else if (self.dataType == PayloadToken.SELL) {
      self.satoshiAmountSell = unpack(dataPart[28 : 28 + 8]);
    }

    return self;
  }

  static function dump(PayloadTokenData self) : bytes {
    bytes payload = b'';
    if (self.dataType == PayloadToken.MINT) {
      payload = PayloadToken.mintPrefix +
      self.blockHeader + num2bin(self.blockHeight, Util.OutputValueLen) +
      self.dataType;
    }
    else if (self.dataType == PayloadToken.TRANSFER) {
      payload = PayloadToken.transferPrefix +
      self.ownerPkh + num2bin(self.amount, Util.OutputValueLen) +
      self.dataType;
    }
    else if (self.dataType == PayloadToken.SWAP) {
      payload = PayloadToken.swapPrefix +
      self.ownerPkh + num2bin(self.amount, Util.OutputValueLen) +
      self.genesisOutpointSwap + self.genesisOutputIdxSwap + num2bin(self.amountSwap, Util.OutputValueLen) +
      self.dataType;
    }
    else if (self.dataType == PayloadToken.SELL) {
      payload = PayloadToken.sellPrefix +
      self.ownerPkh + num2bin(self.amount, Util.OutputValueLen) +
      num2bin(self.satoshiAmountSell, Util.OutputValueLen) +
      self.dataType;
    }
    return self.codeWithGenesisPart + payload;
  }
}
